-------------------------------------------------------------------------------------------------
-- COMMENTS_INSERT
-------------------------------------------------------------------------------------------------
SET NOCOUNT ON;
SET ANSI_NULLS ON;
SET QUOTED_IDENTIFIER ON;
SET TRANSACTION ISOLATION LEVEL READ COMMITTED;

DECLARE @IS_COMMIT BIT = 0;
-------------------------------------------------------------------------------------------------
USE VideoHosting;

BEGIN TRAN
PRINT N'➕ JOB IS STARTED';

IF NOT EXISTS (SELECT 1 FROM [sys].[tables] WHERE [name] = N'COMMENTS') BEGIN
	PRINT N'❌ TABLE [COMMENTS] WAS NOT FOUND';
END ELSE BEGIN
    -- CREATE VARIABLES
    DECLARE @USERNAME NVARCHAR(32);
    DECLARE @VIDEO NVARCHAR(100);
    
    DECLARE @USER_UID UNIQUEIDENTIFIER;
    DECLARE @VIDEO_UID UNIQUEIDENTIFIER;
    DECLARE @TEXT NVARCHAR(100);

    -- CREATE TMP TABLES
    BEGIN 
        DROP TABLE IF EXISTS #CSV;
        CREATE TABLE #CSV (
            [USERNAME] NVARCHAR(32) NOT NULL,
            [VIDEO] NVARCHAR(100) NOT NULL,
            [TEXT] NVARCHAR(250) NOT NULL,
        );
        BULK INSERT #CSV FROM '/DataCsv/comments.csv'
        WITH (
	        FIRSTROW = 2,
	        FIELDTERMINATOR = ';',
	        ROWTERMINATOR = '\n',
            DATAFILETYPE = 'WideChar'
        );
        PRINT N'➕ CREATE TEMP TABLES IS SUCCESS'
    END;

    -- INSERT TABLE
    BEGIN
        DECLARE CUR CURSOR FOR SELECT [USERNAME], [VIDEO], [TEXT] FROM #CSV;
        OPEN CUR;
        FETCH NEXT FROM CUR INTO @USERNAME, @VIDEO, @TEXT;
        WHILE @@FETCH_STATUS = 0 BEGIN
            SET @USER_UID = (SELECT [UID] FROM [USERS] WHERE [USERNAME] = @USERNAME);
            SET @VIDEO_UID = (SELECT [UID] FROM [VIDEOS] WHERE [TITLE] = @VIDEO);
            MERGE INTO [COMMENTS] AS [COMMENT] 
                USING (VALUES (@USER_UID, @VIDEO_UID, @TEXT)) AS [NEW_COMMENT] ([USER_UID], [VIDEO_UID], [TEXT])
                ON [COMMENT].[USER_UID] = [NEW_COMMENT].[USER_UID] AND [COMMENT].[VIDEO_UID] = [NEW_COMMENT].[VIDEO_UID]
                WHEN NOT MATCHED THEN
                    INSERT ([VIDEO_UID], [USER_UID], [TEXT]) VALUES (@VIDEO_UID, @USER_UID, @TEXT);
            FETCH NEXT FROM CUR INTO @USERNAME, @VIDEO, @TEXT;
        END;
        CLOSE CUR;
        DEALLOCATE CUR;

        PRINT N'➕ INSERT IS SUCCESS'
    END;
    DROP TABLE IF EXISTS #CSV;
    PRINT N'➕ DROP TEMP TABLES IS SUCCESS'
END;


IF (@IS_COMMIT = 1) BEGIN
    COMMIT TRAN
    PRINT N'➕ INSERT IS COMMITTED'
END ELSE BEGIN
    ROLLBACK TRAN
    PRINT N'❌ INSERT IS ROLL-BACKED'
END;
